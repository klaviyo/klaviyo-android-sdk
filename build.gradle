// Top-level build file where you can add configuration options common to all sub-projects/modules.
import static de.fayard.refreshVersions.core.Versions.versionFor

buildscript {
    ext {
        publishBuildVariant = "release"
    }

    repositories {
        google()
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        // NOTE: These are dependencies for the build script itself
        // Do not place your application dependencies here;
        // they belong in the individual module build.gradle files
        classpath Android.tools.build.gradlePlugin
        classpath Kotlin.gradlePlugin
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:_"
        classpath "org.jlleitschuh.gradle:ktlint-gradle:_"
    }
}

plugins {
    //Project-wide dependencies
    id "org.jetbrains.dokka"
    id "org.jlleitschuh.gradle.ktlint"
}

dependencies {
    dokkaHtmlMultiModulePlugin "org.jetbrains.dokka:versioning-plugin:_"
}

tasks.register("clean", Delete) {
    delete rootProject.layout.buildDirectory
}

static def readXmlValue(String filePath, String tagName, Project project) {
    def xmlFile = new File(project.projectDir, filePath)
    // Check if the file exists
    if (!xmlFile.exists()) {
        throw new FileNotFoundException("The XML file does not exist: ${xmlFile.absolutePath}")
    }

    // Parse the XML file
    def xmlContent
    try {
        xmlContent = new XmlSlurper().parse(xmlFile)
    } catch (Exception e) {
        throw new RuntimeException("Failed to parse XML file: ${xmlFile.absolutePath}. Error: ${e.message}", e)
    }

    // Look for the string with the specific name attribute
    def result = xmlContent.'string'.find { it.@name == tagName }

    if (result == null) {
        throw new IllegalArgumentException("No string found with the name '${tagName}' in the file: ${xmlFile.absolutePath}")
    }

    return result.text()
}

dokkaHtmlMultiModule {
    def versionName = readXmlValue('sdk/core/src/main/res/values/strings.xml','klaviyo_sdk_version_override', project)
    def oldVersionsDir = layout.buildDirectory.dir("../docs/")
    outputDirectory = layout.buildDirectory.dir("../docs/${versionName}")
    includes.from("README.md")

    String versioningConfiguration = """
        {
          "version": "${versionName}",
          "olderVersionsDir": "${oldVersionsDir}"
        }
    """

    pluginsMapConfiguration.set(
        ["org.jetbrains.dokka.versioning.VersioningPlugin": versioningConfiguration]
    )
}

/**
 * Command to bump the version of the project
 * including updating README and documentation references
 */
public class BumpVersion extends DefaultTask {
    private String nextVersion;

    @Option(option = "nextVersion", description = "New semantic version number")
    public void setNextVersion(String nextVersion) {
        this.nextVersion = nextVersion;
    }

    @Input
    public String getNextVersion() {
        return nextVersion;
    }

    static String readXmlValue(String filePath, String tagName) {
        def xmlFile = new File(filePath)
        def xmlContent = new XmlSlurper().parse(xmlFile)
        // Look for the string with the specific name attribute
        def result = xmlContent.'string'.find { it.@name == tagName }
        return result?.text() ?: ""
    }

    @TaskAction
    public void bumpVersion() {
        def currentVersion = readXmlValue('sdk/core/src/main/res/values/strings.xml','klaviyo_sdk_version_override')
        println(currentVersion)
        def nextVersion = this.getNextVersion()
        def currentBuild = versionFor(project, "version.klaviyo.versionCode") as Integer
        def nextBuild = currentBuild + 1
        print("Changing semantic version number from $currentVersion to $nextVersion\n")
        print("Auto-incrementing version code from $currentBuild to $nextBuild\n")

        ant.replace(file:"versions.properties", token:"versionCode=$currentBuild", value:"versionCode=$nextBuild")
        def file = new File('sdk/core/src/main/res/values/strings.xml')
        def newName = file.text.replace(currentVersion,nextVersion)
        file.text = newName
        ant.replace(file:"README.md", token:"analytics:$currentVersion", value:"analytics:$nextVersion")
        ant.replace(file:"README.md", token:"push-fcm:$currentVersion", value:"push-fcm:$nextVersion")
        ant.replace(file:"docs/index.html", token:"$currentVersion", value:"$nextVersion")
    }
}

tasks.register("bumpVersion", BumpVersion)
