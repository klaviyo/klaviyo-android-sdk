// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    apply from: 'versions.gradle'

    ext {
        publishBuildVariant = "release"
    }

    repositories {
        google()
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        // NOTE: These are dependencies for the build script itself
        // Do not place your application dependencies here;
        // they belong in the individual module build.gradle files
        classpath Android.tools.build.gradlePlugin
        classpath Kotlin.gradlePlugin
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:_"
        classpath "org.jlleitschuh.gradle:ktlint-gradle:_"
    }
}

plugins {
    //Project-wide dependencies
    id 'org.jetbrains.dokka' version "${dokkaVersion}"
    id "org.jlleitschuh.gradle.ktlint" version "${gradleKtlintVersion}"
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

dependencies {
    dokkaHtmlMultiModulePlugin "org.jetbrains.dokka:versioning-plugin:${dokkaVersion}"
}

tasks.register('clean', Delete) {
    delete rootProject.layout.buildDirectory
}

dokkaHtmlMultiModule {
    outputDirectory = layout.buildDirectory.dir("../docs/${versionName}")
    includes.from("README.md")

    String versioningConfiguration = """
        {
          "version": "${versionName}",
          "olderVersionsDir": "${layout.buildDirectory.dir('../docs/')}"
        }
    """

    pluginsMapConfiguration.set(
        ["org.jetbrains.dokka.versioning.VersioningPlugin": versioningConfiguration]
    )
}
