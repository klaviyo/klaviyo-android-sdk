plugins {
    id "de.fayard.refreshVersions" version "0.60.3"
}

dependencyResolutionManagement {
    repositories {
        google()
        mavenCentral()
        maven { url "https://jitpack.io" }
    }
}

include ":app"

// Composite Build:
// This section can substitute a local copy of the SDK for the remote dependency
// This is useful for testing changes to the SDK locally
// see https://docs.gradle.org/current/userguide/composite_builds.html
// Note: turn off composite build before using refreshVersions
def allowComposite = false
def useInternal = false

if (allowComposite) {
    def sdkDirectory = useInternal ? new File("../klaviyo-android-sdk-internal/") : new File("../klaviyo-android-sdk/")

    if (sdkDirectory.exists()) {
        includeBuild(sdkDirectory) {
            dependencySubstitution {
                // substitute remote dependency with local module
                substitute module("com.github.klaviyo.klaviyo-android-sdk:core") using project(":sdk:core")

                substitute module("com.github.klaviyo.klaviyo-android-sdk:analytics") using project(":sdk:analytics")

                substitute module("com.github.klaviyo.klaviyo-android-sdk:push-fcm") using project(":sdk:push-fcm")
            }
        }
    }
}

refreshVersions {
    rejectVersionIf {
        candidate.stabilityLevel.isLessStableThan(current.stabilityLevel)
    }
}
