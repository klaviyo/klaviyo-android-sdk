import static de.fayard.refreshVersions.core.Versions.versionFor

apply plugin: "com.android.application"

// If you're seeing a failure related to google services, make sure you've added the google-services.json file to your project
apply plugin: "com.google.gms.google-services"

// Add the Crashlytics Gradle plugin
apply plugin: "com.google.firebase.crashlytics"

apply plugin: "kotlin-android"

apply plugin: "org.jlleitschuh.gradle.ktlint"

def localProperties = new Properties()
if (rootProject.file("local.properties").canRead()) {
    localProperties.load(new FileInputStream(rootProject.file("local.properties")))
}

android {
    def javaVersion = JavaVersion.VERSION_11

    namespace "com.klaviyo.sdktestapp"
    compileSdk 34
    buildToolsVersion="34.0.0"
    defaultConfig {
        applicationId "com.klaviyo.sdktestapp"
        minSdkVersion 23
        targetSdkVersion 34
        versionCode 12
        versionName "2.1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    def defaultCompany = localProperties["companyId"] ?: "\"LuYLmF\""

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
            buildConfigField "String", "KLAVIYO_COMPANY_ID", defaultCompany
        }
        debug {
            debuggable = true
            buildConfigField "String", "KLAVIYO_COMPANY_ID", defaultCompany
        }
    }
    compileOptions {
        sourceCompatibility javaVersion
        targetCompatibility javaVersion
    }
    buildFeatures {
        buildConfig = true
        compose true
        viewBinding true
    }
    kotlinOptions {
        jvmTarget = javaVersion.toString()
    }
    composeOptions {
        kotlinCompilerExtensionVersion = versionFor(project, AndroidX.compose.compiler)
    }
}

ktlint {
    verbose = true
    android = true
    ignoreFailures = false
    outputToConsole = true
    outputColorName = "RED"
    disabledRules = ["max-line-length"]
    filter {
        exclude("**/generated/**")
        include("**/kotlin/**")
        include("**/java/**")
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])

    // Maven SDK dependencies -- You can compile localDebug from the SDK and reference that build variant here
    implementation "com.github.klaviyo.klaviyo-android-sdk:core:_"
    implementation "com.github.klaviyo.klaviyo-android-sdk:analytics:_"
    implementation "com.github.klaviyo.klaviyo-android-sdk:push-fcm:_"

    // Compose
    def composeBom = platform(AndroidX.compose.bom)
    implementation(composeBom)
    androidTestImplementation(composeBom)
    implementation AndroidX.compose.runtime
    implementation AndroidX.compose.ui
    implementation AndroidX.compose.foundation
    implementation AndroidX.compose.foundation.layout
    implementation AndroidX.constraintLayout.compose
    implementation AndroidX.compose.material
    implementation AndroidX.compose.material3
    implementation AndroidX.compose.material.icons.extended
    implementation AndroidX.compose.runtime.liveData
    implementation AndroidX.compose.ui.tooling
    implementation "com.google.accompanist:accompanist-themeadapter-material:_"
    implementation "com.google.accompanist:accompanist-pager:_"
    implementation "com.google.accompanist:accompanist-pager-indicators:_"
    implementation AndroidX.activity.compose
    implementation AndroidX.activity.ktx

    implementation Kotlin.stdlib.jdk8
    implementation AndroidX.appCompat
    implementation AndroidX.core.ktx
    implementation AndroidX.constraintLayout
    implementation AndroidX.navigation.compose
    implementation AndroidX.viewPager2
    implementation Google.android.material

    // Import the BoM for the Firebase platform
    implementation platform(Firebase.bom)
    implementation Firebase.cloudMessagingKtx //FCM for push notifications
    implementation Firebase.crashlyticsKtx
    implementation Firebase.analyticsKtx

    testImplementation Testing.junit4
    androidTestImplementation AndroidX.test.runner
    androidTestImplementation AndroidX.test.espresso.core

    implementation "org.jetbrains.kotlin:kotlin-reflect:_"
}
