apply plugin: 'com.android.application'

// gotta apply those google services for fcm
apply plugin: 'com.google.gms.google-services'

// Add the Crashlytics Gradle plugin
apply plugin: 'com.google.firebase.crashlytics'

apply plugin: 'kotlin-android'

apply plugin: "org.jlleitschuh.gradle.ktlint"

def localProperties = new Properties()
if (rootProject.file("local.properties").canRead()) {
    localProperties.load(new FileInputStream(rootProject.file("local.properties")))
}

android {
    compileSdkVersion 33
    buildToolsVersion "33.0.0"
    defaultConfig {
        applicationId "com.klaviyo.sdktestapp"
        minSdkVersion 23
        targetSdkVersion 33
        versionCode 5
        versionName "1.2.0-beta"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    def defaultCompany = localProperties['companyId'] ?: '"LuYLmF"'

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField "String", "KLAVIYO_COMPANY_ID", defaultCompany
        }
        debug {
            debuggable = true
            buildConfigField "String", "KLAVIYO_COMPANY_ID", defaultCompany
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    buildFeatures {
        viewBinding true
        compose true
    }
    kotlinOptions {
        jvmTarget = "11"
    }
    composeOptions {
        kotlinCompilerExtensionVersion "1.3.2"
    }
}

ktlint {
    verbose = true
    android = true
    ignoreFailures = false
    outputToConsole = true
    outputColorName = "RED"
    disabledRules = ["max-line-length"]
    filter {
        exclude("**/generated/**")
        include("**/kotlin/**")
        include("**/java/**")
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Maven SDK dependencies -- You can compile localDebug from the SDK and reference that build variant here
    implementation 'com.github.klaviyo.klaviyo-android-sdk:core:1.2.0-beta'
    implementation 'com.github.klaviyo.klaviyo-android-sdk:analytics:1.2.0-beta'
    implementation 'com.github.klaviyo.klaviyo-android-sdk:push-fcm:1.2.0-beta'

    // Compose
    def composeBom = platform('androidx.compose:compose-bom:2022.10.00')
    implementation(composeBom)
    androidTestImplementation(composeBom)
    implementation "androidx.compose.runtime:runtime"
    implementation "androidx.compose.ui:ui"
    implementation "androidx.compose.foundation:foundation"
    implementation "androidx.compose.foundation:foundation-layout"
    implementation "androidx.constraintlayout:constraintlayout-compose:1.0.1"
    implementation "androidx.compose.material:material"
    implementation "androidx.compose.material3:material3:1.0.1"
    implementation "androidx.compose.material:material-icons-extended"
    implementation "androidx.compose.runtime:runtime-livedata"
    implementation "androidx.compose.ui:ui-tooling"
    implementation "com.google.accompanist:accompanist-themeadapter-material:0.28.0"
    implementation "com.google.accompanist:accompanist-pager:0.28.0"
    implementation "com.google.accompanist:accompanist-pager-indicators:0.28.0"
    implementation "androidx.activity:activity-compose:1.6.1"
    implementation 'androidx.activity:activity-ktx:1.6.1'

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.5.1'
    implementation 'androidx.core:core-ktx:1.9.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation "androidx.navigation:navigation-compose:2.5.3"
    implementation 'androidx.viewpager2:viewpager2:1.0.0'
    implementation 'com.google.android.material:material:1.7.0'

    // Import the BoM for the Firebase platform
    implementation platform('com.google.firebase:firebase-bom:31.4.0')
    implementation "com.google.firebase:firebase-messaging-ktx" //FCM for push notifications
    implementation 'com.google.firebase:firebase-crashlytics-ktx'
    implementation 'com.google.firebase:firebase-analytics-ktx'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test:runner:1.5.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.0'

    implementation "org.jetbrains.kotlin:kotlin-reflect:1.7.20"
}
