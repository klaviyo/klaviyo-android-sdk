description = "Mobile Inbox functionality for the Klaviyo SDK suite"
evaluationDependsOn(":sdk")

val publishBuildVariant: String by rootProject.extra
val readXmlValue: (String, String, Project) -> String by rootProject.extra
val klaviyoGroupId: String by project

android {
    namespace = "$klaviyoGroupId.inbox"

    publishing {
        singleVariant(publishBuildVariant) {
            withSourcesJar()
            withJavadocJar()
        }
    }
}

dependencies {
    api(project(":sdk:inbox-core"))
    implementation(project(":sdk:core"))
    implementation(project(":sdk:analytics"))
    implementation(project(":sdk:push-fcm"))
    implementation(platform(Firebase.bom))
    implementation(Firebase.cloudMessaging)
    implementation(KotlinX.coroutines.core)
    testImplementation(project(":sdk:fixtures"))
}

afterEvaluate {
    publishing {
        publications {
            create<MavenPublication>("release") {
                from(components[publishBuildVariant])
                groupId = klaviyoGroupId
                artifactId = "inbox"
                version = readXmlValue(
                    "src/main/res/values/strings.xml",
                    "klaviyo_sdk_version_override",
                    project(":sdk:core")
                )
            }
        }
    }
}
