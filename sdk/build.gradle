import static de.fayard.refreshVersions.core.Versions.versionFor

subprojects {
    apply plugin: "com.android.library"
    apply plugin: "kotlin-android"
    apply plugin: "org.jlleitschuh.gradle.ktlint"
    apply plugin: "org.jetbrains.dokka"
    apply plugin: "maven-publish"

    android {
        compileSdkVersion versionFor(project, "version.android.compileSdk") as Integer
        buildToolsVersion versionFor(project, "version.android.buildTools") as String

        def localProperties = new Properties()
        if (rootProject.file("local.properties").canRead()) {
            localProperties.load(new FileInputStream(rootProject.file("local.properties")))
        }
        def serverTarget = localProperties["localKlaviyoServerUrl"] ?: klaviyoServerUrl
        def apiRevision = localProperties["klaviyoApiRevision"] ?: klaviyoApiRevision
        def cdnUrl = localProperties["localKlaviyoCdnUrl"] ?: klaviyoCdnUrl
        def assetSource = localProperties["klaviyoAssetSource"] ?: ""

        defaultConfig {
            minSdkVersion versionFor(project, "version.android.minSdk") as Integer
            targetSdkVersion versionFor(project, "version.android.targetSdk") as Integer
            testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
            versionCode versionFor(project, "version.klaviyo.versionCode") as Integer
            versionName readXmlValue('src/main/res/values/strings.xml','klaviyo_sdk_version_override', project(':sdk:core'))
            consumerProguardFiles("consumer-rules.pro")
        }

        buildFeatures {
            buildConfig = true
        }

        compileOptions {
            sourceCompatibility = javaVersion
            targetCompatibility = javaVersion
        }

        kotlinOptions {
            jvmTarget = javaVersion.toString()
        }

        buildTypes {
            release {
                minifyEnabled false
                proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
                buildConfigField "String", "KLAVIYO_SERVER_URL", "\"${serverTarget}\""
                buildConfigField "String", "KLAVIYO_API_REVISION", "\"${apiRevision}\""
                buildConfigField "String", "KLAVIYO_CDN_URL", "\"${cdnUrl}\""
                buildConfigField "String", "KLAVIYO_ASSET_SOURCE", "\"${assetSource}\""
            }
            debug {
                debuggable = true
                buildConfigField "String", "KLAVIYO_SERVER_URL", "\"${serverTarget}\""
                buildConfigField "String", "KLAVIYO_API_REVISION", "\"${apiRevision}\""
                buildConfigField "String", "KLAVIYO_CDN_URL", "\"${cdnUrl}\""
                buildConfigField "String", "KLAVIYO_ASSET_SOURCE", "\"${assetSource}\""
            }
        }

        testOptions {
            unitTests.all {
                // JVM Arguments required for various unit test mocks via Mockk as of Java 17
                jvmArgs(
                        "--add-opens", "java.base/java.lang=ALL-UNNAMED",
                        "--add-opens", "java.base/java.lang.reflect=ALL-UNNAMED",
                        "--add-opens", "java.base/java.net=ALL-UNNAMED",
                        "--add-opens", "java.base/javax.net.ssl=ALL-UNNAMED",
                        "--add-opens", "java.base/sun.net.www.protocol.https=ALL-UNNAMED",
                        "--add-exports", "java.base/sun.net.www.protocol.https=ALL-UNNAMED",
                )
            }
        }

        lint {
            // Fail the build if any lint errors are found
            abortOnError = true

            // Check release builds for issues
            checkReleaseBuilds = true

            // Treat NewApi errors as fatal (API level compatibility)
            error 'NewApi'

            // Generate HTML report for local review
            htmlReport = true
        }
    }

    dependencies {
        /***** Main Dependencies *****/
        //Kotlin Dependencies
        implementation AndroidX.core.ktx
        implementation Kotlin.stdlib.jdk8

        /***** Instrumentation Testing Dependencies *****/
        androidTestImplementation AndroidX.test.runner
        androidTestImplementation AndroidX.test.espresso.core
    }

    ktlint {
        verbose = true
        android = true
        ignoreFailures = false
        outputToConsole = true
        outputColorName = "RED"
        disabledRules = ["max-line-length"]
        filter {
            exclude("**/generated/**")
            include("**/kotlin/**")
            include("**/java/**")
        }
    }

    dokkaHtmlPartial {
        dokkaSourceSets {
            configureEach {
                includeNonPublic.set(false)
                reportUndocumented.set(true)
                skipEmptyPackages.set(true)
                suppressInheritedMembers.set(true)

                jdkVersion.set(17)
            }
        }
    }
}
