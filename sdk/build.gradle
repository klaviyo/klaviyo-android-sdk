subprojects {
    def ext = rootProject.ext

    apply plugin: "com.android.library"
    apply plugin: "kotlin-android"
    apply plugin: "org.jlleitschuh.gradle.ktlint"
    apply plugin: "org.jetbrains.dokka"
    apply plugin: "maven-publish"

    android {
        compileSdkVersion ext.androidCompileVersion
        buildToolsVersion ext.buildToolsVersion

        def localProperties = new Properties()
        if (rootProject.file("local.properties").canRead()) {
            localProperties.load(new FileInputStream(rootProject.file("local.properties")))
        }
        def serverTarget = localProperties['localKlaviyoServerUrl'] ?: klaviyoServerUrl

        defaultConfig {
            minSdkVersion ext.minSDKVersion
            targetSdkVersion ext.targetSDKVersion
            testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
            versionCode ext.versionCode
            versionName ext.versionName
        }

        buildFeatures {
            buildConfig = true
        }

        buildTypes {
            release {
                minifyEnabled false
                proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
                buildConfigField "String", "KLAVIYO_SERVER_URL", "\"${serverTarget}\""
                buildConfigField "String", "VERSION", "\"${ext.versionName}\""
            }
            debug {
                debuggable = true
                buildConfigField "String", "KLAVIYO_SERVER_URL", "\"${serverTarget}\""
                buildConfigField "String", "VERSION", "\"${ext.versionName}\""
            }
        }

        testOptions {
            unitTests.all {
                jvmArgs(
                        "--add-opens", "java.base/java.lang=ALL-UNNAMED",
                        "--add-opens", "java.base/java.lang.reflect=ALL-UNNAMED"
                )
            }
        }
    }

    dependencies {
        /***** Main Dependencies *****/
        //Kotlin Dependencies
        implementation "androidx.core:core-ktx:$ext.coreKTXVersion"
        implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$ext.kotlinSTDVersion"

        /***** Instrumentation Testing Dependencies *****/
        androidTestImplementation "androidx.test:runner:$ext.testRunnerVersion"
        androidTestImplementation "androidx.test.espresso:espresso-core:$ext.espressoVersion"
    }

    ktlint {
        verbose = true
        android = true
        ignoreFailures = false
        outputToConsole = true
        outputColorName = "RED"
        disabledRules = ["max-line-length"]
        filter {
            exclude("**/generated/**")
            include("**/kotlin/**")
            include("**/java/**")
        }
    }

    dokkaHtmlPartial {
        dokkaSourceSets {
            configureEach {
                includeNonPublic.set(false)
                reportUndocumented.set(true)
                skipEmptyPackages.set(true)
                suppressInheritedMembers.set(true)

                jdkVersion.set(17)
            }
        }
    }
}
