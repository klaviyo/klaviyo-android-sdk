subprojects {
    apply plugin: 'com.android.library'
    apply plugin: 'kotlin-android'
    apply plugin: "org.jlleitschuh.gradle.ktlint"
    apply plugin: 'org.jetbrains.dokka'

    def ext = rootProject.ext

    def localProperties = new Properties()
    if (rootProject.file("local.properties").canRead()) {
        localProperties.load(new FileInputStream(rootProject.file("local.properties")))
    }

    android {
        compileSdkVersion ext.androidCompileVersion
        buildToolsVersion ext.buildToolsVersion

        defaultConfig {
            minSdkVersion ext.minSDKVersion
            targetSdkVersion ext.targetSDKVersion
            testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
            versionCode ext.versionCode
            versionName ext.versionName
        }

        buildTypes {
            release {
                minifyEnabled false
                proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
                buildConfigField "String", "KLAVIYO_SERVER_URL", '"https://a.klaviyo.com"'
            }
            debug {
                buildConfigField "String", "KLAVIYO_SERVER_URL", '"https://a.klaviyo.com"'
            }
            localDebug {
                buildConfigField "String", "KLAVIYO_SERVER_URL", "\"" + localProperties['localAddress'] + "\""
            }
        }
    }

    dependencies {
        /***** Main Dependencies *****/
        //Kotlin Dependencies
        implementation "androidx.core:core-ktx:$ext.coreKTXVersion"
        implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$ext.kotlinSTDVersion"

        /***** Testing Dependencies *****/
        testImplementation "junit:junit:$ext.junitVersion"
        testImplementation "androidx.test:runner:$ext.testRunnerVersion"
        testImplementation "androidx.test:rules:$ext.testRulesVersion"
        // Mocking Dependencies
        testImplementation "io.mockk:mockk:$ext.mockkVersion"
        // JSON is bundled with the Android SDK so we need real objects to test against
        testImplementation "org.json:json:$ext.jsonVersion"

        /***** Instrumentation Testing Dependencies *****/
        androidTestImplementation "androidx.test:runner:$ext.testRunnerVersion"
        androidTestImplementation "androidx.test.espresso:espresso-core:$ext.espressoVersion"
    }

    ktlint {
        verbose = true
        android = true
        ignoreFailures = false
        outputToConsole = true
        outputColorName = "RED"
        disabledRules = ["max-line-length"]
        filter {
            exclude("**/generated/**")
            include("**/kotlin/**")
            include("**/java/**")
        }
    }

    dokkaHtmlPartial {
        dokkaSourceSets {
            configureEach {
                includeNonPublic.set(false)
                reportUndocumented.set(true)
                skipEmptyPackages.set(true)
                suppressInheritedMembers.set(true)

                jdkVersion.set(11)
            }
        }
    }
}